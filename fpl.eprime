language ESSENCE' 1.0

given gwCount : int
given numPlayers : int
given teamSize : int
given maxPoints : int
given minPrice : int
given maxPrice : int
given priceLimit : int

letting playerDom be domain int(1..numPlayers)
letting pointsDom be domain int(0..maxPoints)
letting gwDom be domain int(1..gwCount)
letting teamDom be domain int(1..teamSize)
letting positionDom be domain int(0..3)
letting priceDom be domain int(minPrice..maxPrice)
letting clubDom be domain int(0..19)
letting subsDom be domain int(1..4)

given points : matrix indexed by [playerDom, gwDom] of pointsDom
given positions : matrix indexed by [playerDom] of positionDom
given prices : matrix indexed by [playerDom] of priceDom
given clubs : matrix indexed by [playerDom] of clubDom
 
find team : matrix indexed by [teamDom] of playerDom
find teamPoints : matrix indexed by [teamDom, gwDom] of pointsDom
find totalPoints : int(0..maxPoints * teamSize * gwCount)
find teamPositions : matrix indexed by [teamDom] of positionDom
find teamPrices : matrix indexed by [teamDom] of priceDom
find totalPrice : int(minPrice * teamSize..maxPrice * teamSize)
find teamClubs : matrix indexed by [teamDom] of clubDom
find subs : matrix indexed by [subsDom, gwDom] of teamDom

maximising totalPoints

such that
    $ Calculate team points
    forAll i : teamDom .
        forAll j : gwDom .
            teamPoints[i, j] = points[team[i], j],

    $ Calculate total points
    totalPoints = sum i : gwDom .
         (max(teamPoints[.., i])) +
         (sum j : teamDom .
            teamPoints[j, i]) -
         (sum k : subsDom .
            teamPoints[subs[k, i], i]),

    $ Calculate team positions
    forAll i : teamDom .
        teamPositions[i] = positions[team[i]],

    $ Calculate team prices
    forAll i : teamDom .
        teamPrices[i] = prices[team[i]],

    $ Calculate total price
    totalPrice =
        sum i : teamDom .
            teamPrices[i],

    $ Calculate team clubs
    forAll i : teamDom .
        teamClubs[i] = clubs[team[i]],

    $ Price limit
    totalPrice <= priceLimit,
    $totalPrice >= 950,

    $ Ensure correct position counts & player ordering (by GW1 score)
    teamPositions[1] = 0,
    teamPositions[2] = 0,
    team[1] > team[2],

    teamPositions[3] = 1,
    teamPositions[4] = 1,
    teamPositions[5] = 1,
    teamPositions[6] = 1,
    teamPositions[7] = 1,
    team[3] > team[4],
    team[4] > team[5],
    team[5] > team[6],
    team[6] > team[7],

    teamPositions[8] = 2,
    teamPositions[9] = 2,
    teamPositions[10] = 2,
    teamPositions[11] = 2,
    teamPositions[12] = 2,
    team[8] > team[9],
    team[9] > team[10],
    team[10] > team[11],
    team[11] > team[12],
    $team[12] = 1,

    teamPositions[13] = 3,
    teamPositions[14] = 3,
    teamPositions[15] = 3,
    team[13] > team[14],
    team[14] > team[15],

    $ Ensure 3 players per club
    atmost(teamClubs, [3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3,3], [0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19]),

    $ Select subs
    forAll i : gwDom .
        allDiff(subs[.., i]) /\
        subs[1, i] in int(1,2) /\
        teamPoints[subs[1, i], i] <= teamPoints[1, i] /\
        teamPoints[subs[1, i], i] <= teamPoints[2, i] /\
        subs[2, i] in int(3..15) /\
        subs[3, i] in int(3..15) /\
        subs[4, i] in int(3..15) /\
        teamPoints[subs[2, i], i] <= teamPoints[subs[3, i], i] /\
        teamPoints[subs[3, i], i] <= teamPoints[subs[4, i], i] /\
        forAll j : int(3..15) .
            (j != subs[2, i] /\ j != subs[3, i]) ->
                teamPoints[subs[4, i], i] <= teamPoints[j, i]
